// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.21' apply false
}

// This block encapsulates custom properties and makes them available to all
// modules in the project.
ext {
    // Kotlin
    kotlinVersion = '1.8.21'
    // Jetpack Compose
    // Kotlin + Compose + Accompanist must all be compatible with each other
    // See Compose BOM to lib mapping: https://developer.android.com/jetpack/compose/bom/bom-mapping
    // See Compose to Kotlin compat: https://developer.android.com/jetpack/androidx/releases/compose-kotlin
    composeBOMVersion = '2023.05.01'
    composeVersion = '1.4+'
}

allprojects {
    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'org.jetbrains.kotlin' &&
                    (requested.name == 'kotlin-stdlib' || requested.name == 'kotlin-stdlib-jdk8')) {
                details.useVersion kotlinVersion
            }
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = "1.8"
    }
}